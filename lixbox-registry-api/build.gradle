import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*
plugins {
    id 'com.bmuschko.docker-remote-api' version '6.1.3'
    id 'io.quarkus' version "${quarkus_gradle_plugins}"
}
project(":lixbox-registry-api"){
    repositories {
        mavenCentral()
    }
    dependencies {
        compile enforcedPlatform("io.quarkus:quarkus-bom:${quarkusVersion}")
        
        //project
//        sans objet
        
        //lixbox
        compile(group: 'fr.lixbox.lixbox-common', name: 'lixbox-core', version: version_lixbox_common)
        compile(group: 'fr.lixbox.lixbox-service', name: 'lixbox-microservice-common', version: version_lixbox_service){ 
            exclude(group:'fr.lixbox')
        }
        
        //quarkus
        compile group: 'io.quarkus', name: 'quarkus-resteasy'
        compile group: 'io.quarkus', name: 'quarkus-resteasy-jackson'

        //divers
        compile group: 'redis.clients', name: 'jedis', version: '3.2.0'
        compile group: 'jakarta.ejb', name: 'jakarta.ejb-api', version: '3.2.6'
        
        //TEST
        testCompile group:'io.quarkus', name: 'quarkus-junit5'
        testCompile group:'io.rest-assured', name: 'rest-assured', version: '3.3.0'
        testCompile group: 'io.quarkus', name: 'quarkus-rest-client', version: quarkusVersion
    }
    
    eclipse{
        project{
            natures 'org.jboss.tools.ws.jaxrs.nature'
        }
        wtp{
            facet {
                facet name: 'jst.cdi', version: '2.0'
                facet name: "jst.jaxrs", version: "2.1"
            }   
        }
    }
    
    sonarqube {
        properties {
           property "sonar.projectKey", "${config.desc_project.artefact.group}.${rootProject.name}:${project.name}"
           property "sonar.projectName", "${project.name}"
           property "sonar.sources","src/main"
           property "sonar.tests","src/test, src/integTest"
        }
    }
    
    jar{
       duplicatesStrategy = DuplicatesStrategy.INCLUDE
       from('dist') {
          include '**/*.*'
            into 'META-INF/resources'
       }
   
       manifest {
            attributes(
                "Implementation-Title": project.name, 
                "Implementation-Version": archiveVersion, 
                "Implementation-Timestamp": new Date()
            )
        }
    }

    test {
      useJUnitPlatform()
      environment "REGISTRY_REDIS_URI", "tcp://main.host:6379"
    }

    
    //creation de l'image docker
    task pullOpenjdk11Image(type: DockerPullImage, description: "Pull openjdk11 image"){
       image='openjdk:11'
    }    
    task syncDependencies(type: Sync) {
        dependsOn quarkusBuild
        from "build/${project.name}-${project.version}-runner.jar"
        into 'build/docker'
    }
    tasks.create('createDockerfile', Dockerfile) {
        dependsOn pullOpenjdk11Image,syncDependencies
        from('openjdk:11')
        copyFile("${project.name}-${project.version}-runner.jar", "/app/${project.name}-${project.version}-runner.jar")
        environmentVariable("JAVA_OPTS",'${JAVA_OPTS:--Xms=256M}')
        runCommand("useradd lixbox --create-home --shell /bin/bash")
        runCommand("chown -R lixbox /app")
        user("lixbox")
        entryPoint('java')
        workingDir('/app')
        defaultCommand('-jar', "${project.name}-${project.version}-runner.jar")
        exposePort(18100)
    }
    task buildProjectImages(type: DockerBuildImage, group: "docker", description: "build project images") {
        dependsOn createDockerfile
        buildArgs.put("APP_VERSION","$project.version")
        buildArgs.put("GRAYLOG_ENV","$config.desc_project.artefact.project")
        images.add("${config.desc_project.artefact.dockerImageKey}/${project.name}:$project.version")
        images.add("${config.desc_project.artefact.dockerImageKey}/${project.name}:latest")
    }
    task pushProjectImages(type: DockerPushImage, group: "docker", description: "push project images") {
       registryCredentials {
            username = project.hasProperty("dockerhub_username")?project.getProperty("dockerhub_username"):''
            password = project.hasProperty("dockerhub_password")?project.getProperty("dockerhub_password"):''
        }
        images.add("${config.desc_project.artefact.dockerImageKey}/${project.name}:$project.version")
        images.add("${config.desc_project.artefact.dockerImageKey}/${project.name}:latest")
    }
    pushProjectImages.dependsOn buildProjectImages
    buildAndPushDockerImages.dependsOn pushProjectImages


    //initialisation du conteneur REDIS pour les tests
    task pullRedisImage(type: DockerPullImage, description: "Pull redis image"){
       image='redis:latest'
    }
    task removeRedisContainer(type: DockerRemoveContainer, description: "Remove redis container"){
        targetContainerId 'registry_serv_redis_test'
        onError { exception ->
            if (!(exception.message.contains('No such container')||exception.message.contains('is already in progress')))
                throw exception
        }
    }
    task createRedisContainer(type: DockerCreateContainer, description: "Create a redis container for test") {
        targetImageId 'redis:latest'
        containerName = 'registry_serv_redis_test'
        hostConfig.autoRemove = true
        hostConfig.portBindings = ['6379:6379']
    }
    task startRedisContainer(type: DockerStartContainer, group: "docker", description: "start redis container") {
        targetContainerId "registry_serv_redis_test"
    }   
    task stopRedisContainer(type: DockerStopContainer, group: "docker", description: "stop redis container") {
        targetContainerId "registry_serv_redis_test"        
        onError { exception ->
            if (!exception.message.contains('No such container'))
                throw exception
        }
    }
    tasks.clean.dependsOn(tasks.pullRedisImage);
    tasks.createRedisContainer.dependsOn(tasks.removeRedisContainer);
    tasks.removeRedisContainer.dependsOn(tasks.stopRedisContainer);
    tasks.startRedisContainer.dependsOn(tasks.createRedisContainer);
    tasks.test.dependsOn(tasks.startRedisContainer);
    tasks.quarkusDev.dependsOn(tasks.startRedisContainer);
}