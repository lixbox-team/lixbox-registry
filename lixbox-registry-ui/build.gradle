import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*
import java.nio.file.Paths;
plugins {
    id 'com.bmuschko.docker-remote-api' version "${docker_remote_plugins}"
    id "com.github.node-gradle.node" version "3.1.0"
    id 'io.quarkus' version "${quarkus_gradle_plugins}"
}
project(':lixbox-registry-ui'){
    repositories {
        mavenCentral()
    }
    dependencies {
        //project
//        sans objet
        
        
        //lixbox
        api(group: 'fr.lixbox.lixbox-common', name: 'lixbox-stream', version: version_lixbox_common)
        api(group: 'fr.lixbox.lixbox-io', name: 'lixbox-json', version: version_lixbox_io)
        
        
        //quarkus
        api group: 'io.quarkus', name: 'quarkus-undertow', version: quarkusVersion
        api group: 'io.quarkus', name: 'quarkus-reactive-routes', version: quarkusVersion
        
        
        //TEST
        testImplementation group:'io.quarkus', name: 'quarkus-junit5', version: quarkusVersion
        testImplementation group:'io.rest-assured', name: 'rest-assured', version: '3.3.0'
        testImplementation group: 'io.quarkus', name: 'quarkus-rest-client', version: quarkusVersion
    }
    
    jar{
       duplicatesStrategy = DuplicatesStrategy.INCLUDE
       from('dist') {
          include '**/*.*'
            into 'META-INF/resources'
       }
   
       manifest {
            attributes(
                "Implementation-Title": project.name, 
                "Implementation-Version": archiveVersion, 
                "Implementation-Timestamp": new Date()
            )
        }
    }
    
    eclipse{
        wtp{
            facet {
                facet name: 'jst.cdi', version: '2.0'
            }   
        }
    }
    
    sonarqube {
        properties {
           property "sonar.projectKey", "${config.desc_project.artefact.group}.${rootProject.name}:${project.name}"
           property "sonar.projectName", "${project.name}"
           property "sonar.sources","src/main"
           property "sonar.tests","src/test, src/integTest"
        }
    }
    
    dependencyCheck.skip=true

    test {
      useJUnitPlatform()
    }

    node {
        version = version_node
        npmVersion = version_npm
        download = true
    }
    
    npm_run_build {
        inputs.files fileTree('public')
        inputs.files fileTree('src')
    
        inputs.file 'package.json'
    }
    
    task copyNpm(type: Copy) {
        from('dist') {
            include '**/*.*'
        }
        into 'build/classes/java/main/META-INF/resources'
    }    
    copyNpm.dependsOn npm_run_build
    jar.dependsOn copyNpm
    quarkusDev.dependsOn copyNpm

    
    //export quarkus project runner to maven
    def quarkusRunner = file("$buildDir/libs/${project.name}-${version}.rjar")
    def quarkusRunnerArtifact = artifacts.add('archives', quarkusRunner) {
        type 'rjar'
        builtBy 'quarkusBuild'
    }    
    task copyArtefact(type: Copy) {
        from "$buildDir/${project.name}-${version}-runner.jar"
        into "$buildDir/libs"
        rename { String fileName -> "${project.name}-${version}.rjar"}
    }
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact quarkusRunnerArtifact
            }
        }
    }
    tasks.quarkusBuild.finalizedBy(copyArtefact);
    tasks.jar.finalizedBy(quarkusBuild);
    
    
    //creation de l'image docker
    task pullOpenjdk11Image(type: DockerPullImage, description: "Pull openjdk11 image"){
       image='openjdk:11'
    }    
    task syncDependencies(type: Sync) {
        dependsOn quarkusBuild
        from "build/${project.name}-${project.version}-runner.jar"
        into 'build/docker'
    }
    tasks.create('createDockerfile', Dockerfile) {
        dependsOn pullOpenjdk11Image,syncDependencies
        from('openjdk:11')
        copyFile("${project.name}-${project.version}-runner.jar", "/app/${project.name}-${project.version}-runner.jar")
        environmentVariable("JAVA_OPTS",'${JAVA_OPTS:--Xms=256M}')
        runCommand("useradd lixbox --create-home --shell /bin/bash")
        runCommand("chown -R lixbox /app")
        user("lixbox")
        entryPoint('java')
        workingDir('/app')
        defaultCommand('-jar', "${project.name}-${project.version}-runner.jar")
        exposePort(18100)
    }
    task buildProjectImages(type: DockerBuildImage, group: "docker", description: "build project images") {
        dependsOn createDockerfile
        images.add("${config.desc_project.artefact.dockerImageKey}/${project.name}:$project.version")
        images.add("${config.desc_project.artefact.dockerImageKey}/${project.name}:latest")
    }
    task pushProjectImages(type: DockerPushImage, group: "docker", description: "push project images") {
       registryCredentials {
            username = project.hasProperty("dockerhub_username")?project.getProperty("dockerhub_username"):''
            password = project.hasProperty("dockerhub_password")?project.getProperty("dockerhub_password"):''
        }
        images.add("${config.desc_project.artefact.dockerImageKey}/${project.name}:$project.version")
        images.add("${config.desc_project.artefact.dockerImageKey}/${project.name}:latest")
    }
    pushProjectImages.dependsOn buildProjectImages
    buildAndPushDockerImages.dependsOn pushProjectImages
    tasks.clean.dependsOn tasks.npmInstall
    tasks.nodeSetup.dependsOn {
        def rc = Paths.get(project.buildDir.canonicalPath, '.npmrc').toFile()
        if(rc.exists())
        {
            rc.delete()
            rc << "strict-ssl=false\n"
        }
    }
}